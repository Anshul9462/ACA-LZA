targetScope = 'subscription'

// ------------------
//    PARAMETERS
// ------------------

@description('The location where the resources will be created.')
param location string = deployment().location

@minLength(2)
@maxLength(8)
@description('The name of the workloard.')
param workloadName string

@minLength(1)
@maxLength(4)
@description('The name of the environment (e.g. "dev", "test", "prod").')
param environmentName string

@minLength(2)
@maxLength(6)
@description('The short name of the location (e.g. "westus2" becomes "wus2", "northeurope" becomes "northeu" or "ne").')
param locationShortName string

@minLength(1)
@maxLength(90)
@description('Optional. The name of the resource group to create the resources in. If set, it overrides the name generated by the template.')
param hubResourceGroupName string = 'rg-${workloadName}-${environmentName}-${locationShortName}'

@description('Optional. The tags to be assigned to the created resources.')
param tags object = {}

// Virtual Network
@minLength(2)
@maxLength(64)
@description('Optional. The name of the virtual network to create for the hub. If set, it overrides the name generated by the template.')
param hubVNetName string = 'vnet-${workloadName}-${environmentName}-${locationShortName}'

@description('CIDR of the Hub Virtual Network.')
param vnetAddressPrefixes array

// Bastion
@description('Enable or disable the creation of the Azure Bastion.')
param enableBastion bool

@minLength(1)
@maxLength(80)
@description('Optional. The name of the bastion host to create. If set, it overrides the name generated by the template.')
param bastionName string = 'bas-${workloadName}-${environmentName}-${locationShortName}'

@minLength(1)
@maxLength(80)
@description('Optional. The name of the network security group to create for the Azure Bastion. If set, it overrides the name generated by the template.')
param bastionNSGName string = 'nsg-bas-${workloadName}-${environmentName}-${locationShortName}'

@description('CIDR to use for the Azure Bastion subnet.')
param bastionSubnetAddressPrefix string

@minLength(1)
@maxLength(80)
@description('Optional. The name of the public IP address to create for the Azure Bastion. If set, it overrides the name generated by the template.')
param bastionPublicIpName string = 'pip-bas-${workloadName}-${environmentName}-${locationShortName}'

// Virtual Machine
@minLength(1)
@maxLength(64)
@description('Optional. The name of the virtual machine (jumpbox) to create. If set, it overrides the name generated by the template.')
param vmName string = 'vm-${workloadName}-${environmentName}-${locationShortName}'

@description('The size of the virtual machine to create. See https://docs.microsoft.com/en-us/azure/virtual-machines/sizes for more information.')
param vmSize string

@description('The username to use for the virtual machine.')
param vmAdminUsername string

@secure()
@description('The password to use for the virtual machine.')
param vmAdminPassword string

@secure()
@description('The SSH public key to use for the virtual machine.')
param vmLinuxSshAuthorizedKeys string

@allowed(['linux', 'windows', 'none'])
@description('The type of the virtual machine OS to create. If set to "none", no virtual machine will be created.')
param vmJumpboxOSType string = 'none'

@minLength(1)
@maxLength(80)
@description('Optional. The name of the network security group to create for the virtual machine. If set, it overrides the name generated by the template.')
param vmNSGName string = 'nsg-vm-${workloadName}-${environmentName}-${locationShortName}'

@minLength(1)
@maxLength(80)
@description('Optional. The name of the subnet to create for the virtual machine. If set, it overrides the name generated by the template.')
param vmSubnetName string = 'snet-jumpbox'

@description('CIDR to use for the virtual machine subnet.')
param vmJumpBoxSubnetAddressPrefix string

@minLength(1)
@maxLength(80)
@description('Optional. The name of the network interface to create for the virtual machine. If set, it overrides the name generated by the template.')
param vmNICName string = 'nic-vm-${workloadName}-${environmentName}-${locationShortName}'

// ------------------
// VARIABLES
// ------------------

// => Subnet definition taking in consideration feature flags
var defaultSubnets = []

// This cannot be another value
var bastionSubnetName = 'AzureBastionSubnet'

// Append optional bastion subnet, if required
var subnets = enableBastion ? concat(defaultSubnets, [
  {
    name: bastionSubnetName
    properties: {
      addressPrefix: bastionSubnetAddressPrefix
    }
  }
]) : defaultSubnets

//Append optional jumpbox subnet, if required
var vnetSubnets = vmJumpboxOSType != 'none' ? concat(subnets, [
  {
    name: vmSubnetName
    properties: {
     addressPrefix: vmJumpBoxSubnetAddressPrefix       
    }        
  }
]) : subnets

// ------------------
// RESOURCES
// ------------------

resource hubResourceGroup 'Microsoft.Resources/resourceGroups@2021-04-01' = {
  name: hubResourceGroupName
  location: location
  tags: tags
}

module hubVNet '../modules/vnet.bicep' = {
  name: take('${deployment().name}-hub-vnet', 64)
  scope: hubResourceGroup
  params: {
    vnetName: hubVNetName
    location: location
    subnets: vnetSubnets
    vnetAddressPrefixes: vnetAddressPrefixes
  }
}

module bastion './modules/bastion.bicep' = if (enableBastion) {
  name: take('${deployment().name}-bastion', 64)
  scope: hubResourceGroup
  params: {
    location: location
    tags: tags
    bastionName: bastionName
    bastionNetworkSecurityGroupName: bastionNSGName
    bastionPublicIpName: bastionPublicIpName
    bastionSubnetName: bastionSubnetName
    bastionSubnetAddressPrefix: bastionSubnetAddressPrefix
    bastionVNetName: hubVNet.outputs.vnetName
  }
 }

module jumpboxLinuxVM './modules/vm/linux-vm.bicep' = if (vmJumpboxOSType == 'linux') {
  name: take('${deployment().name}-linux-vm', 64)
  scope: hubResourceGroup
  params: {
    location: location
    tags: tags
    vmName: vmName
    vmAdminUsername: vmAdminUsername
    vmAdminPassword: vmAdminPassword
    vmSshPublicKey: vmLinuxSshAuthorizedKeys
    vmSize: vmSize

    vmVnetName: hubVNet.outputs.vnetName
    vmSubnetName: vmSubnetName
    vmSubnetAddressPrefix: vmJumpBoxSubnetAddressPrefix
    vmNetworkInterfaceName: vmNICName
    vmNetworkSecurityGroupName: vmNSGName
  }
}

module jumpboxWindowsVM './modules/vm/windows-vm.bicep' = if (vmJumpboxOSType == 'windows') {
  name: take('${deployment().name}-windows-vm', 64)
  scope: hubResourceGroup
  params: {
    location: location
    tags: tags
    vmName: vmName
    vmAdminUsername: vmAdminUsername
    vmAdminPassword: vmAdminPassword
    vmSize: vmSize

    vmVnetName: hubVNet.outputs.vnetName
    vmSubnetName: vmSubnetName
    vmSubnetAddressPrefix: vmJumpBoxSubnetAddressPrefix
    vmNetworkInterfaceName: vmNICName
    vmNetworkSecurityGroupName: vmNSGName
  }
}

// ------------------
// OUTPUTS
// ------------------

@description('The resource ID of hub virtual network.')
output hubVNetId string = hubVNet.outputs.vnetId
